def root = rootProject.ext
if (root.isDebug) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'dagger.hilt.android.plugin'
android {
    compileSdkVersion root.android.compileSdkVersion
    buildFeatures {
        viewBinding true
    }
    defaultConfig {
        if (root.isDebug) {
            applicationId root.applicationid.login
        }
        minSdkVersion root.android.minSdkVersion
        targetSdkVersion root.android.targetSdkVersion
        versionCode root.android.versionCode
        versionName root.android.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [AROUTER_MODULE_NAME: project.getName()]
            }
        }
//        resourcePrefix "login_"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            if (root.isDebug) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
}

dependencies {
    implementation project(':lib_common')
    implementation project(path: ':lib_application')
    implementation project(path: ':lib_utils')

    annotationProcessor root.libARouterCompiler

    implementation root.hilt
    annotationProcessor root.compiler
}